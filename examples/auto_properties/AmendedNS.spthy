theory AmendedNS
begin

section{* The Amended Needham Schroeder Symmetric Key Protocol *}

text{*
  Modelled according to the desciprtion in the SPORE library:

    http://www.lsv.ens-cachan.fr/Software/spore/nssk_amended.html

  Notable differences:

    1. Instead of assuming a typed model we are using global constants to
       ensure distinctness of messages from different steps.

    2. As we do not support reasoning about encrypted tickets yet, we are using
       a hack: Namely, we allow the recipient to check the structure of the 
       received message. However, we are using free variables such that no
       accidental equalities are introduced.

    3. We use the weaker authentication property of non-injective agreement
       instead of injective agreement because the later cannot yet be proven
       automatically. However, based on the resulting Isabelle file it would be
       easy to also derive injective agreement from the proven non-injective
       variants.
*}

protocol AmendedNS
{
   1. I -> R: I

   2.   <- R: {'1', I, nr}k(R,S)
      I <-  : TicketR

   3. I ->  : I, R, ni, TicketR
        -> S: I, R, ni, {'1', I, nr}k(R,S)

   4.   <- S: {'2', ni, R, kIR, {'3', kIR,     nr,     I}k(R,S)}k(I,S)
      I <-  : {'2', ni, R, kIR, {'3', SomekIR, Somenr, I}k(R,S)}k(I,S)

   5. I ->  : {'3', SomekIR, Somenr, I}k(R,S)
        -> R: {'3', kIR, nr, I}k(R,S)

   6. I <- R: {'4', nr}kIR

   7. I -> R: {'5', h(nr)}kIR
}


subsection{* Security Properties *}

properties (of AmendedNS)
  auto: auto-properties
  
  I_kir_sec: secret(I, 7, kIR, {I,R,S})
  R_kir_sec: secret(R, 7, kIR, {I,R,S})

property (of AmendedNS) I_ni_agree:
  premises
    "role(1) = I"
    "step(1, I_7)"
    "uncompromised(I#1, R#1, S#1)"
  imply threads 2 ,3 such that
    "  role(2) = R
     & role(3) = S
     & I#1  = I#2     & I#2  = I#3
     & R#1  = R#2     & R#2  = R#3
     & S#1  = S#2     & S#2  = S#3
                      & ni#1 = ni#3    // the responder never receives ni
     & nr#1 = nr#2    & nr#2 = nr#3  
     & kIR#1 = kIR#2  & kIR#2 = kIR#3
   "

property (of AmendedNS) R_ni_agree:
  premises
    "role(2) = R"
    "step(2, R_7)"
    "uncompromised(I#2, R#2, S#2)"
  imply threads 1 ,3 such that
    "  role(1) = I
     & role(3) = S
     & I#1  = I#2     & I#2  = I#3
     & R#1  = R#2     & R#2  = R#3
     & S#1  = S#2     & S#2  = S#3
                      & ni#1 = ni#3    // the responder never receives ni
     & nr#1 = nr#2    & nr#2 = nr#3  
     & kIR#1 = kIR#2  & kIR#2 = kIR#3
   "


end


