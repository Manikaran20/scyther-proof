theory WideMouthFrog
begin

section{* Wide Mouthed Frog *}

text{*
  The protocol is modeled after the description in the SPORE library:

    http://www.lsv.ens-cachan.fr/Software/spore/wideMouthedFrog.html

  Note that we cannot reason about the timestamps in our current model.
  Furthermore, our current calculus for backwards reasoning is too weak
  to reason about the original protocol

    1: I -> S: I, {TimeI, R, kIR}k(I,S)
    2: R <- S:    {TimeS, I, kIR}k(R,S)

  because the server S could receive the message {TimeI, R, kIR}k(I,S)
  arbitrarily many times from himself. Therefore, we added global constants
  identifying the different steps.
*}

protocol WMF
{
  1. I -> S: I, {'step1', TimeI, R, kIR}k(I,S)
  2. R <- S:    {'step2', TimeS, I, kIR}k(R,S)
}


subsection{* Security Properties *}

properties (of WMF)
  auto: auto-properties

  I_kIR_sec: secret(I, 1, kIR, {I,R,S}) 
  R_kIR_sec: secret(R, 2, kIR, {I,R,S}) 

property (of WMF) R_ni_agree:
  premises
    "role(2) = R"
    "step(2, R_2)"
    "uncompromised(I#2, R#2, S#2)"
  imply threads 1, 3 such that
    " role(1) = I
    & role(3) = S
    & I#1 = I#2      & I#2 = I#3
    & R#1 = R#2      & R#2 = R#3
    & S#1 = S#2      & S#2 = S#3
    & TimeI#1 = TimeI#3
    & TimeS#2 = TimeS#3
    & kIR#1 = kIR#2  & kIR#2 = kIR#3 "


end
